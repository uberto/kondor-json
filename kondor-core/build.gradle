plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    useJUnitPlatform()
    maxHeapSize = "2g"
    testLogging {
//        showStandardStreams = true
        events "skipped", "failed", "passed"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    api project(":kondor-outcome")

    testImplementation project(":kondor-tools")
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testImplementation "io.strikt:strikt-core:$striktVersion"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:$junitLauncherVersion"

    testImplementation 'org.leadpony.justify:justify:3.1.0'
    runtimeOnly 'org.leadpony.joy:joy-classic:2.0.0'
}


archivesBaseName = 'kondor-core'

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = archivesBaseName
            version = project.version

            pom {
                name = 'kondor-core'
                description = 'A Kotlin library to use Json in functional way without reflection'
                url = 'https://github.com/uberto/kondor-json'
                inceptionYear = '2021'
                scm {
                    url = 'https://github.com/uberto/kondor-json'
                    connection = 'https://github.com/uberto/kondor-json.git'
                    developerConnection = 'git@github.com:uberto/kondor-json.git'
                }
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'UbertoBarbini'
                        name = 'Uberto Barbini'
                        email = 'uberto.gama@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            url = version.endsWith('SNAPSHOT') ? 'https://oss.sonatype.org/content/repositories/snapshots/' : 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = findProperty('nexusUsername')
                password = findProperty('nexusPassword')
            }
        }
    }
}

signing {
    sign publishing.publications
}

javadoc {
    options.addBooleanOption('html5', true)
}